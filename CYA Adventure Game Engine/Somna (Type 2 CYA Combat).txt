

import dice as d

# CHARACTER SHEET

table player
| "attribute"  | "value" |
| "strength"   | 0       |
| "arcane"     | 0       |
| "health"     | 0       |
| "experience" |  0      |
end

# TABLES OF ENCOUNTERS

table monsters
| "name"      | "type"        | "modifier"   |
| "goblin"    | "strength"    | 2            |
| "wolf"      | "strength"    | 3            |
| "wizard"    | "arcane"      | 3            |
end

table monsters2
| "name"            | "type"        | "modifier"   |
| "barking toad"    | "arcane"      | 1            |
end

overlay "levelup"
	# Shorthand vals.
	p_str = (player.get "strength")
	p_arc = (player.get "arcane")
	p_hea = (player.get "health")
	p_exp = (player.get "experience")

	"Select an attribute to increase."
	["Strength" 
		(p_str.set "value" (p_str.get "value")+1)
		(p_exp.set "value" (p_exp.get "value")-3)
		(say "You've increased your strength to" (p_str.get "value"))
		exit]
	["Arcane" 
		(p_arc.set "value" (p_arc.get "value")+1)
		(p_exp.set "value" (p_exp.get "value")-3)
		(say "You've increased your arcane to" (p_arc.get "value"))
		exit]
	["Health" 
		(p_hea.set "value" (p_hea.get "value")+1)
		(p_exp.set "value" (p_exp.get "value")-3)
		(say "You've increased your health to " (p_hea.get "value"))
		exit]
	["Back" 
		(say "No experience spent.")
		exit]

overlay "menu" access "m"
	"Menu: enter 'm' to exit."
	["See stats" (say player)]
	["Level up"
		(say "You may spend 3 experience points to increase one of your attributes by 1.")
		[if (p_exp.get "value") < 3
			then (say "You don't have enough experience right now.")
			else (say "triggered levelup") run "levelup"
		]
	]
	["Save" (save)]
	["Load" (load)]
end


START "opening"

scene "opening"
	# Shorthand vals.
	p_str = (player.get "strength")
	p_arc = (player.get "arcane")
	p_hea = (player.get "health")
	p_exp = (player.get "experience")

	name = (ask "What is your name?: ")
	(say name", you are a computer science student and have angered Somnus, the god of sleep, by staying up too late!")
	"You must fight your way through his hoardes using a combination of your strength and arcane powers to finally get a good night's rest."
	
	points = 6
	(say "You have "points" points to spend on your two attributes: strength and arcane power.")
	
	str_choice = (ask "How many points do you want to spend on strength? The rest will be assigned to arcane (0-"points"): ")
	[	
		if !(isnum str_choice)
		then 
			(say "Invalid, you must enter a number.") -> "opening"
		else 
			str_choice = (num str_choice)
			[
				if str_choice >= 0 and str_choice <= 6
				then (p_str.set "value" str_choice)(p_arc.set "value" points-str_choice)(p_hea.set "value" 5)
					["Continue" 
						-> "1" 
						trigger_battle = true
						(say "Successfully assigned the following values: Strength: "(p_str.get "value")" & Arcane: "(p_arc.get "value")".")
					]
				else 
					(say "Invalid entry. Must be in range 0-6")
					-> "opening"
			]
	]

scene "1"
	back = "1"
	[
	if trigger_battle
	then
		(say "The moon is high in the sky. The stars are out. You should be asleep but instead you've been hunched over your laptop for the past several hours. Its time to reclaim your sleep.")
		(say "You storm out the door, flashlight in hand, gum in pocket, and big O notation in mind.")
		(say "You hear a rustling to your right.")
		["Investigate rustling" 
			monster = (monsters.getrandom)
			run "battle"
		]
		["Ignore it" 
			(say "You ignore the rustling and continue on your way. You hear a growl behind you.")
			monster = (monsters2.getrandom)
			run "battle"
		]
	else
		(say "Your first fight is already over. Well done for surviving.")
		["Head north towards the scary twisty woods" -> "twisty woods"]
		["Head south towards the riverbank" -> "riverbank"]
			
	]
	

overlay "battle"
	trigger_battle = false
	enemy_name = (monster.get "name")
	type = (monster.get "type")
	enemy_mod = (monster.get "modifier")
	(say "Oh no! A " enemy_name"! It looks like it wants to fight!")
	(say enemy_name" has "type": "enemy_mod)

	player_mod = ((player.get type).get "value")
	(say "your "type": " player_mod)

	["Fight!"
		m_score = (d.roll "1d6")+enemy_mod
		p_score = (d.roll "1d6")+player_mod
		(say enemy_name" got: "m_score". You got: "p_score)
		[if p_score > m_score
			then (say "You defeated the "enemy_name"!")
				(say "You gain "enemy_mod" experience.")
				(p_exp.set "value" (p_exp.get "value")+enemy_mod)
				-> back

			else (say "You were defeated by the "enemy_name"!")
				(say "You lose 1 health.")
				(p_hea.set "value" (p_hea.get "value")-1)
				[if (p_hea.get "value") <= 0
					then (say "You have no health left. You are defeated.")
						-> "gameover"
					else (say "The monster loses interest in you and leaves.")
						-> back
				]

		]
	]
	["Run!!!"
		[if player_mod > enemy_mod
			then (say "You run away safely.")
				-> back
			else (say "You can't run away from a monster stronger than you.")
		]
	]


scene "gameover"
	"You have been defeated by Somnus' minions. You'll never get a good night's sleep."
	["Restart" -> "opening"]


scene "twisty woods"
	"Well, this is the twisty woods."


scene "riverbank"
	"This is the riverbank."